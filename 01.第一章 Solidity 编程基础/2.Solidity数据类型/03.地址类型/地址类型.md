# 地址类型

Solidity 中，使用地址类型来表示一个账号，地址类型有两种形式。

```
address：保存一个 20 字节的值（以太坊地址的大小）。
```

```
address payable：表示可支付地址，与 address 相同也是 20 字节，不过它有成员函数transfer()和 send()。
```

这种区别背后的思想是 address payable 可以接受以太币的地址，而一个普通的 address 则不能，不过在使用的时候，大部分时间操作者不需要关注 address 和 address payable，一般使用 address 就好，如果遇到编译问题，需要一个 address payable，可以使用以下方式进行转换：

```solidity
address payable ap = payable(addr);
```

**提示：上面的转换方法是在 Solidity 0.6 版本加入，如果是 Solidity 0.5 版本，则使用 address payable ap = address(uint160(addr))；可以看出，address 可以显式地和整型进行转换，除此之外，address 还可以显式地与 bytes20（20 个字节长度的字节数组）和合约类型之间进行相互转换 。**

当被转换的地址是一个是合约地址时，需要合约实现接收（receive）函数或具有 payable 修饰的回退（fallback）函数（这是两个特殊定义的函数，在后面小节会详细介绍），才能显式和 address payable 类型相互转换 （转换仍然使用 address(addr) 执行），如果合约没有接收或 payable 回退函数，则需要进行两次转换，将 payable(address(addr))转换为 address payable 类型 。

地址类型支持的比较运算包括： <=、<、==、!=、>= 以及 >。常用的还是判断两个地址是相等（==）还是不相等（!=）。

**地址类型成员**

地址类型和整型等基本类型不同，地址类型还有自己的成员属性及函数。

```
<address>.balance(uint256):
```

balance 成员属性：返回地址类型 address 的余额，余额以 wei 为单位。

```
<address payable>.transfer(uint256 amount):
```

transfer 成员函数，向地址发送特定数量（以单位 wei 为单位，用参数 amount 指定 ）的以太币，失败时抛出异常，消耗固定的 2300 gas 。

```
<address payable>.send(uint256 amount) returns (bool):
```

send 成员函数：向地址发送特定数量（以单位 wei 为单位，用参数 amount 指定）的以太币，失败时返回 false，消耗固定的 2300 gas。实际上 addr.transfer(y)与 require(addr.send(y))是等价的。

**注意:** send()是 transfer()的低级版本。如果执行失败，当前的合约不会因为异常而终止，在使用 send()的时候，如果不检查返回值，会有风险。大部分情况下应该用 transfer()。地址类型使用示例：

```solidity
pragma solidity >0.5.0; 
contract testAddr { 
// 如果合约的余额大于等于 10，而 x 小于 10,则给 x 转 10 wei 
function testTrasfer(address payable x) public { 
address myAddress = address(this); 
if (x.balance < 10 && myAddress.balance >= 10) { 
x.transfer(10); 
}
 }
  }
```

外部账号和合约本质是一样的，每一个合约也是它自己的类型，如上代码中的 testAddr 就是一个合约类型，它也可以转化为地址类型，上面代码的 addressmyAddress = address(this); 就是把合约转换为地址类型，然后用.balance 获取余额。

这里有一个很多开发者忽略的知识点：如果给一个合约地址转账，即上面代码 x 是合约地址时，合约的 receive 函数或 fallback 函数会随着 transfer 调用一起执行（这个是 EVM 特性），而 send()和 transfer()的执行只会使用 2300 gas，因此在接收者是一个合约地址的情况下，很容易出现 receive 函数或 fallback 函数把 gas 耗光而出现转账失败的情况。

**为了避免 gas 不足导致转账失败的情况，可以使用下面介绍的底层函数 call()，使用 addr.call{value:1 ether}("")来进行转账，这句代码在功能上等价于 addr.transfer(y)，但 call 调用方式会用上当前交易所有可用的 gas。**

地址类型还有 3 个更底层的成员函数，通常用于与合约交互。

- .call(bytes memory) returns (bool, bytes memory)
- .delegatecall(bytes memory) returns (bool, bytes memory)
- .staticcall(bytes memory) returns (bool, bytes memory)

这 3 个函数用直接控制的编码[给定有效载荷（payload）作为参数]与合约交互，返回成功状态及数据，默认发送所有可用 gas。它是向另一个合约发送原始数据，支持任何类型、任意数量的参数。每个参数会按规则（接口定义 ABI 协议）打包成 32 字节并拼接到一起。Solidity 提供了全局的函数 abi.encode、abi.encodePacked、abi.encodeWithSelector 和 abi.encodeWithSignature 用于编码结构化数据。

例如，下面的代码是用底层方法 call 调用合约 register 方法 。

```solidity
bytes memory payload = abi.encodeWithSignature("register(string)","MyName"); 
(bool success, bytes memory returnData) = address(nameReg).call(payload);
require(success);
```

**注意：所有这些函数都是低级函数，应谨慎使用。因为操作者在调用一个合约的同时就将控制权交给了被调合约，当操作者对一个未知的合约进行这样的调用时，这个合约可能是恶意的，并且被调合约又可以回调操作者（的合约），这可能发生重入攻击（被调合约回调操作者合约，引起操作者合约出现状态错误的一种攻击。**

与其他合约交互的常规方法是在合约对象上调用函数，如 x 是合约对象，f() 是合约内实现的函数，那么 x.f() 就表示调用合约对应的函数。

底层函数还可以通过 value 选项附加发送 ether（delegatecall 不支持.value()），如上面用来避免转账失败的方法：addr.call{value:1 ether}("")。

下面则表示调用函数 register()时，同时存入 1eth。

```solidity
address(nameReg).call{value:1 ether}(abi.encodeWithSignature("register(string)", "MyName"));
```

底层函数还可以通过 gas 选项控制的函数调用使用 gas 的数量，例如下面代码表示调用 register()函数仅有 1000000 gas 可以使用：

```solidity
address(nameReg).call{gas: 1000000}(abi.encodeWithSignature("register(string)", "MyName"));
```

它们还可以联合使用，出现的顺序不重要。

```solidity
address(nameReg).call{gas: 1000000, value: 1 ether}(abi.encodeWithSignature("register(string)", "MyName"));
```

使用函数 delegatecall()也是类似的方式，delegatecall 被称为“委托调用”，顾名思义，是把一个功能委托到另一个合约，它使用当前合约（发起调用的合约）的上下文环境（如存储状态，余额 等），同时使用另一个合约的函数。 delegatecall()多用于调用库代码以及合约升级。
